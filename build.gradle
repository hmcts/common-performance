plugins {
    id 'scala'
    id 'java-library'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.gatling:gatling-app:3.14.3'
    implementation 'org.scala-lang:scala-library:2.13.11'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.azure:azure-security-keyvault-secrets:4.9.4'
    implementation 'com.azure:azure-identity:1.16.1'
    implementation 'org.jsoup:jsoup:1.20.1'
    testImplementation 'org.scalatest:scalatest_2.13:3.2.17'
}

tasks.register('scalaTest', JavaExec) {
    group = "verification"
    description = "Runs ScalaTest tests using the ScalaTest runner"

    classpath = sourceSets.test.runtimeClasspath
    mainClass.set("org.scalatest.tools.Runner")

    args = [
            "-R", "build/classes/scala/test",
            "-oD"
    ]
}

//disable the default test task as the scalaTest task will be used instead
tasks.named("test") {
    enabled = false
}

//runs scalaTest on build
tasks.named("check") {
    dependsOn tasks.named("scalaTest")
}