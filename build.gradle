plugins {
    id 'scala'
    id 'java-library'
    id 'org.owasp.dependencycheck' version '12.1.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencyCheck {
    suppressionFile = "owasp/owasp-suppressions.xml"
    analyzers.assemblyEnabled = false
}

dependencies {
    implementation 'io.gatling:gatling-app:3.14.4'
    implementation 'org.scala-lang:scala-library:2.13.16'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.azure:azure-security-keyvault-secrets:4.10.2'
    implementation 'com.azure:azure-identity:1.18.0'
    implementation 'org.jsoup:jsoup:1.21.2'
    testImplementation 'org.scalatest:scalatest_2.13:3.2.19'
}

tasks.register('scalaTest', JavaExec) {
    group = "verification"
    description = "Runs ScalaTest tests using the ScalaTest runner"

    classpath = sourceSets.test.runtimeClasspath
    mainClass.set("org.scalatest.tools.Runner")

    args = [
            "-R", "build/classes/scala/test",
            "-oD"
    ]
}

//disable the default test task as the scalaTest task will be used instead
tasks.named("test") {
    enabled = false
}